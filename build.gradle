buildscript {
    repositories { jcenter() }
    dependencies { classpath 'org.koin:koin-gradle-plugin:2.2.1' }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.20'
    id 'io.gitlab.arturbosch.detekt' version '1.15.0-RC1'
    id 'org.jlleitschuh.gradle.ktlint' version '9.4.1'
    id 'org.jetbrains.dokka' version '1.4.20'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'koin'

    group = dist_group
    version = dist_version
    archivesBaseName = "Project Essentials ${dist_module_name.capitalize()}"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://kotlin.bintray.com/kotlinx/' }
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.20'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
        implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1'
        implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.1.1'
        implementation 'org.koin:koin-core:2.2.1'
        testImplementation 'org.koin:koin-test:2.2.1'
        testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-assertions-core-jvm:4.3.1'
        testImplementation 'io.kotest:kotest-property-jvm:4.3.1'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    }

    test { useJUnitPlatform() }

    detekt {
        toolVersion = '1.12.0'
        input = files 'src/main/kotlin', 'gensrc/main/kotlin'
        config = files '../config/detekt/detekt.yml'
        parallel = true
        buildUponDefaultConfig = true
        disableDefaultRuleSets = false
        debug = false
        ignoreFailures = false
        reports {
            html {
                enabled = true
                destination = file 'build/reports/detekt.html'
            }
        }
    }

    ktlint {
        outputColorName = 'GREEN'
        enableExperimentalRules = true
        additionalEditorconfigFile = file('..\\.editorconfig')
        filter {
            exclude '**/generated/**'
            include '**/kotlin/**'
        }
    }

    sourceCompatibility = targetCompatibility =
            compileJava.sourceCompatibility =
                    compileJava.targetCompatibility = '1.8'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
        }
    }
}
